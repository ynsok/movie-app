apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

configurations {
    detekt
    ktlint
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.movieapp"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    androidExtensions {
        experimental = true
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:preference-v7:28.0.0'

    //Retrofit2
    implementation "com.squareup.retrofit2:retrofit:2.5.0"
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2"

    //Okhttp3
    implementation "com.squareup.okhttp3:okhttp:3.12.0"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.0'

    //Navigation Component
    implementation 'android.arch.navigation:navigation-fragment-ktx:1.0.0'
    implementation 'android.arch.navigation:navigation-ui-ktx:1.0.0'

    //Kodein
    implementation 'org.kodein.di:kodein-di-generic-jvm:6.1.0'
    implementation 'org.kodein.di:kodein-di-framework-android-support:6.1.0'

    //Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'

    //Room
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    kapt "android.arch.persistence.room:compiler:1.1.1"

//    LiveCycle
    implementation "android.arch.lifecycle:extensions:1.1.1"
    kapt "android.arch.lifecycle:compiler:1.1.1"

    //Constraint Layout
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    detekt "io.gitlab.arturbosch.detekt:detekt-cli:1.0.0.RC9.2"
    ktlint "com.github.shyiko:ktlint:0.29.0"
    implementation "me.cassiano:ktlint-html-reporter:0.2.0"
    implementation 'com.android.support:recyclerview-v7:28.0.0'
}

task detekt(type: JavaExec) {
    main = "io.gitlab.arturbosch.detekt.cli.Main"
    classpath = configurations.detekt
    def input = "$projectDir"
    def config = "../detekt.yml"
    def filters = ".*/build/.*,.*/resources/.*"
    def report = "html:$buildDir/reports/detekt.html"
    def params = ['-i', input, '-c', config, '-f', filters, '--report', report]
    args(params)
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    args "--reporter=plain"
    args "--reporter=html,artifact=me.cassiano:ktlint-html-reporter:0.2.0,output=build/reports/ktlint.html"
}

check.dependsOn(ktlint)
